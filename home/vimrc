"
"
"
"
"
"
"             _____
"         ___/O   O\____
"        / O        O   \
"        \______________/
"   -===|____\///\\\/_____
"       \----------------/
"        \______________/  \/
"         /\__________/    //
"  >=o\  // //\\   || \\  //
"     \\o/ //  \o  ||  \o//
"         //    || ||
"     /o==o     |o \o==o
"    //         //     \\
"    /\        //       /\          csexton's vimrc
"              /\            - frimmin on the frim fram -
"
"
" Bundles {{{
" Pathogen is required for any of these plugins to work
"=bundle justinmk/vim-sneak
"=bundle Shougo/vimproc.vim build: "make -f make_mac.mak", branch: "joe"
"=bundle Shougo/unite.vim
"=bundle csexton/trailertrash.vim
"=bundle csexton/vim-markdown
"=bundle ervandew/ag
"=bundle ervandew/supertab
"=bundle kchmck/vim-coffee-script
"=bundle mattn/gist-vim
"=bundle mattn/webapi-vim
"=bundle pangloss/vim-javascript
"=bundle sjl/gundo.vim
"=bundle slim-template/vim-slim
"=bundle tpope/vim-bundler
"=bundle tpope/vim-endwise
"=bundle tpope/vim-fugitive
"=bundle tpope/vim-pathogen
"=bundle tpope/vim-rails
"=bundle tpope/vim-rake
"=bundle tpope/vim-haml # for scss
"=bundle tpope/vim-repeat
"=bundle tpope/vim-sensible
"=bundle tpope/vim-surround
"=bundle tpope/vim-unimpaired
"=bundle vim-ruby/vim-ruby
"=bundle tpope/vim-eunuch
"=bundle csexton/spacemanspiff.vim
"
"=bundle thoughtbot/vim-rspec
" bundle csexton/jslint.vim
" bundle kana/vim-textobj-user
" bundle nelstrom/vim-textobj-rubyblock
" bundle tomtom/tlib_vim
" bundle tpope/vim-commentary
" bundle tpope/vim-rvm
"
" }}}

" Pathogen {{{
source ~/.vim/bundle/vim-pathogen/autoload/pathogen.vim
execute pathogen#incubate()
syntax on
filetype plugin indent on
" }}}
" Settings {{{
"
let mapleader = ","
set t_Co=256
"colorscheme base16-tomorrow
"silent! colorscheme Tomorrow-Night
silent! colorscheme spacemanspiff

"" Crazyness!
imap jk <Esc>

set fillchars=vert:\ ,fold:-
set nofoldenable
set number
set nowrap

set guifont=Anonymous\ Pro:h14,Monaco:h14
"set guifont=Menlo\ for\ Powerline:h14
"set guifont=Menlo:h14

"au BufEnter * cal ctrlp#SetWorkingPath(2)

let g:pathogen_disabled = ["ack.vim"]

if has("gui_macvim")
  nmap <SwipeLeft> :bN<CR>
  nmap <SwipeRight> :bn<CR>
endif

cnoremap %% <C-R>=expand('%:h').'/'<cr>

"set clipboard=unnamed
" Copy to the system clipboard when in visual mode
vnoremap y "+y
vnoremap <M-y> "+y

" Remap semicolon to be colon
"nmap ; :

" from settings.vim

set cmdheight=1             " Command like height is forced to one line
set dictionary+=/usr/share/dict/words
set gdefault                " When on, the ":substitute" flag 'g' is default on.
set guioptions=egmtc        " Get rid of the scrollbar, toolbar, and popups
set hidden                  " Allow dirty unsaved buffers
set history=1000            " Remember more history
set hlsearch                " Highlight searches
set ignorecase              " Ignore case in searches.
set laststatus=2            " Always show status line
set list                    " show trailing whiteshace and tabs
set listchars=tab:»\ ,trail:\ ,extends:→,precedes:←,nbsp:‗ " Show whitespace glyphs
set modelines=5             " Debian likes to disable this
set mousemodel=popup        " Right mouse button pops up a menu
"set relativenumber
set spelllang=en_us         " Make vim speak 'merican
set suffixes+=.dvi          " Lower priority in wildcards
"set tags+=../tags,../../tags,../../../tags,../../../../tags
set timeoutlen=1200         " A little bit more time for macros
set virtualedit=block       " Let the cursor go where there is nothing
set visualbell              " No more ding sounds
set winwidth=80             " Keep vsplits at least 80 cols

" Wildmenu completion
set wildmenu
set wildmode=list:longest
set wildmode=longest:full,full
set wildignore+=*~
set wildignore+=.hg,.git,.svn                    " Version control
set wildignore+=*.aux,*.out,*.toc                " LaTeX intermediate files
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg   " binary images
set wildignore+=*.o,*.obj,*.exe,*.dll,*.manifest " compiled object files
set wildignore+=*.spl                            " compiled spelling word lists
set wildignore+=*.sw?                            " Vim swap files
set wildignore+=*.DS_Store                       " OSX bullshit
set wildignore+=*.luac                           " Lua byte code
set wildignore+=migrations                       " Django migrations
set wildignore+=*.pyc                            " Python byte code
set wildignore+=*.orig                           " Merge resolution files

" Split Settings
"set splitbelow
"set splitright

" Backups
set noswapfile                    " It's 2012, Vim.
set backupskip=/tmp/*,/private/tmp/*" " Make Vim able to edit crontab files again.
set backupskip+=*.tmp,crontab.*

" Persistant Undo
if has('persistent_undo')
  "set undodir^=~/.vim/undo
  "set undodir^=/Users/csexton/.vim/undo
  set undodir=~/.vim/undo
  set undofile
  set undolevels=1000               "maximum number of changes that can be undone
  set undoreload=10000              "maximum number lines to save for undo on a buffer reload
endif

if has("gui_running")
  "set showbreak=⤥
  set background=dark
endif

" Set the grep program
if &grepprg ==# 'grep -n $* /dev/null'
  set grepprg=grep\ -rnH\ --exclude='.*.swp'\ --exclude='*~'\ --exclude='*.log'\ --exclude=tags\ $*\ /dev/null
endif

" Load matchit
runtime! plugin/matchit.vim
runtime! macros/matchit.vim

"if $TERM == '^\%(screen\|xterm-color\)$' && t_Co == 8
"  set t_Co=16
"endif

command! -bar -nargs=0 SudoW   :setl nomod|silent exe 'write !sudo tee % >/dev/null'|let &mod = v:shell_error
command! -bar -nargs=* -bang W :write<bang> <args>

iabbrev lorem Lorem ipsum dolor sit amet, consectetur adipisicing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua. Ut enim ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut aliquip ex ea commodo consequat. Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum
iabbrev o_o ಠ_ಠ

augroup settings
  autocmd!

  " Restore cursor position
  autocmd BufReadPost *.rb if line("'\"") > 1 && line("'\"") <= line("$") |
      \   exe "normal! g`\"" | endif

  autocmd CursorHold,BufWritePost,BufReadPost,BufLeave *
        \ if isdirectory(expand("<amatch>:h")) | let &swapfile = &modified | endif

  "autocmd BufNewFile,BufRead *.scss             set ft=scss.css
  autocmd BufNewFile,BufRead *.md               set ft=markdown
  autocmd BufNewFile,BufRead *.haml,*.jst       set ft=haml
  autocmd BufNewFile,BufRead *.slim             set ft=slim
  autocmd BufNewFile,BufRead *.feature,*.story  set ft=cucumber
  autocmd BufNewFile,BufRead *.coffee           setl foldmethod=indent nofoldenable
  autocmd BufRead * if ! did_filetype() && getline(1)." ".getline(2).
        \ " ".getline(3) =~? '<\%(!DOCTYPE \)\=html\>' | setf html | endif

  autocmd FileType javascript,coffee            setlocal et sw=2 sts=2 isk+=$
  autocmd FileType html,xhtml,css,scss,scss.css setlocal et sw=2 sts=2 isk+=-
  autocmd FileType eruby,yaml,ruby              setlocal et sw=2 sts=2
  autocmd FileType cucumber                     setlocal et sw=2 sts=2
  autocmd FileType gitcommit                    setlocal spell
  autocmd FileType ruby                         setlocal comments=:#\  tw=79
  autocmd FileType vim                          setlocal et sw=2 sts=2 keywordprg=:help
  autocmd FileType python                       setlocal sts=4 ts=4 shiftwidth=4 textwidth=79
  autocmd FileType c                            setlocal et sw=2 sts=2
  autocmd FileType objc,objcpp                  setlocal et sw=4 sts=4

  autocmd FileType markdown                     setlocal linebreak formatoptions=1 breakat=\ @-+;:,./?^I

  autocmd Syntax   css  syn sync minlines=50


  " Omni completion.
  autocmd FileType css             setlocal omnifunc=csscomplete#CompleteCSS
  autocmd FileType html,markdown   setlocal omnifunc=htmlcomplete#CompleteTags
  autocmd FileType javascript      setlocal omnifunc=javascriptcomplete#CompleteJS
  autocmd FileType python          setlocal omnifunc=pythoncomplete#Complete
  autocmd FileType xml             setlocal omnifunc=xmlcomplete#CompleteTags
  autocmd FileType ruby            set omnifunc=rubycomplete#Complete
  autocmd FileType ruby            let g:rubycomplete_buffer_loading=1
  autocmd FileType ruby            let g:rubycomplete_classes_in_global=1

  autocmd FileType ruby imap <C-l> <Space>=><Space>

  autocmd BufEnter *.rb syn match error contained "\<binding.pry\>"
  autocmd BufEnter *.rb syn match error contained "\<debugger\>"
  autocmd BufEnter *.coffee syn match error contained "\<debugger\>"

augroup END




" vim:set ft=vim et tw=78 sw=2 encoding=utf-8

" }}}
" Mappings {{{


nnoremap          j gj
nnoremap          k gk
inoremap          <C-C> <Esc>`^
cnoremap          <C-O> <Up>
inoremap          ø <C-O>o
inoremap          <M-o> <C-O>o
inoremap          <C-X><C-@> <C-A>

" Sacrilege: emacs like bindings in command and insert
inoremap          <C-A> <C-O>^
cnoremap          <C-A> <Home>
cnoremap          <C-X><C-A> <C-A>
cnoremap          <C-P> <Up>
cnoremap          <C-N> <Down>
inoremap          <C-k> <C-O>D
cnoremap          <C-k> <C-\>e getcmdpos() == 1 ? '' : getcmdline()[:getcmdpos()-2]<CR>

" Buffer Navigation
" Move to the previous buffer with "gp"
nnoremap gp :bp<CR>
" Move to the next buffer with "gn"
nnoremap gn :bn<CR>
" List all possible buffers with "gl"
nnoremap gl :ls<CR>
" List all possible buffers with "gb" and accept a new buffer argument [1]
nnoremap gb :ls<CR>:b

" Clear incremental search highlight
nnoremap <silent> <C-L> :nohlsearch<CR><C-l>
nnoremap <leader>/ :nohlsearch<CR>

" Toggle spell checking
nnoremap <leader>s :set spell!<CR>

" Inserts the path of the currently edited file into a command
"cmap <C-F> <C-R>=expand("%:p:h") . "/" <CR>

" If at end of a line of spaces, delete back to the previous line. Otherwise, <Left>
inoremap <silent> <C-B> <C-R>=getline('.')=~'^\s*$'&&col('.')>strlen(getline('.'))?"0\<Lt>C-D>\<Lt>Esc>kJs":"\<Lt>Left>"<CR>
cnoremap          <C-B> <Left>

" If at end of line, decrease indent, else <Del>
inoremap <silent> <C-D> <C-R>=col('.')>strlen(getline('.'))?"\<Lt>C-D>":"\<Lt>Del>"<CR>
cnoremap          <C-D> <Del>

" If at end of line, fix indent, else <Right>
inoremap <silent> <C-F> <C-R>=col('.')>strlen(getline('.'))?"\<Lt>C-F>":"\<Lt>Right>"<CR>
cnoremap          <C-F> <Right>

" Map Control-E to go to the end of the line in insert mode. However, when
" completion is active you can use CTRL-E to stop it and go back to the
" originally typed text.
inoremap          <C-E> <C-R>=InsCtrlE()<CR>
function! InsCtrlE()
    try
        norm! i
        return "\<c-o>A"
    catch
        return "\<c-e>"
    endtry
endfunction

" Toggle line wrapping
map <Leader>w  :set nowrap!<CR>

" Toggle line numbers
map <Leader>l  :set nonumber!<CR>

" Browse the directory of the current file
"map <Leader>b  :edit %:h<CR>

" Jump to the last posistion in the buffer
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g`\"" | endif
" Jump back to the top on git commit messages
autocmd BufReadPost COMMIT_EDITMSG exe "normal! gg"
" }}}
" Bundle settings {{{

" smartusline
"set statusline=%<%f\ %h%m%r%=%-14.(%l,%c%V%)\ %P
"let g:smartusline_hi_insert = 'guibg=#6D1F21 guifg=white ctermbg=red ctermfg=black'
"let g:smartusline_hi_normal = 'guibg=#B5BD68 guifg=black ctermbg=green ctermfg=black'

" For sneaky streak
let g:sneak#streak = 1

" For vimux
"map <Leader>p :call VimuxRunCommand("clear; rspec " . bufname("%"))<CR>
"map <Leader>rp :VimuxPromptCommand<CR>
"map <Leader>rl :VimuxRunLastCommand<CR>
"map <Leader>ri :VimuxInspectRunner<CR>
"map <Leader>rx :VimuxClosePanes<CR>
"map <Leader>rq :VimuxCloseRunner<CR>
"map <Leader>rc :VimuxInterruptRunner<CR>

let g:is_bash = 1                " Highlight all .sh files as if they were bash
let g:ruby_minlines = 500
let g:rubycomplete_buffer_loading = 1
let g:rubycomplete_rails = 1

let g:bufExplorerShowRelativePath=1
if has('mac')
  let g:gist_clip_command = 'pbcopy'
endif

" Surround
let g:surround_{char2nr('s')} = " \r"
let g:surround_{char2nr(':')} = ":\r"
let g:surround_indent = 1

" Vim Clojure
let vimclojure#HighlightBuiltins = 1 " Highlight Clojure builtins
let vimclojure#ParenRainbow = 1      " Rainbow parentheses

" SuperTab
let g:SuperTabMappingForward  = '<s-tab>'
let g:SuperTabMappingBackward = '<tab>'

" Fix arrow keys in tmux
map <Esc>[B <Down>

" Launch Markedapp
":nnoremap <leader>m :silent !open -a Marked.app '%:p'<cr>

if !exists('g:bundle_dir') | let g:bundle_dir =  expand('$HOME/.vim/bundle') | endif
if isdirectory(g:bundle_dir)

  map <Leader>g  :GundoToggle<CR>
  map <D-g>  :GundoToggle<CR>
  command! Gundo :GundoToggle

  " Unite
  let g:unite_source_history_yank_enable = 1
  call unite#filters#matcher_default#use(['matcher_fuzzy'])
  call unite#filters#sorter_default#use(['sorter_rank'])
  " sort file results by length
  "call unite#custom#source('file', 'sorters', 'sorter_length')
  "call unite#custom#source('file_rec/async', 'sorters', 'sorter_length')
  " limit results for recently edited files
  "call unite#custom#source('file_mru', 'max_candidates', 10)
  " ignored files for file_mru
  call unite#custom#source('file_mru', 'ignore_pattern', 'COMMIT_EDITMSG')
  " sort buffers by number
  "call unite#custom#source('buffer', 'sorters', 'sorter_reverse')

  nnoremap <leader>t :<C-u>Unite -no-split -buffer-name=files   -start-insert buffer file_rec/async:!<cr>
  nnoremap <leader>f :<C-u>Unite -no-split -buffer-name=files   -start-insert file<cr>
  nnoremap <leader>r :<C-u>Unite -no-split -buffer-name=mru     -start-insert file_mru<cr>
  nnoremap <leader>o :<C-u>Unite -no-split -buffer-name=outline -start-insert outline<cr>
  nnoremap <leader>y :<C-u>Unite -no-split -buffer-name=yank    history/yank<cr>
  nnoremap <leader>e :<C-u>Unite -no-split -buffer-name=buffer  buffer<cr>
  nnoremap <leader>j :<C-u>Unite -no-split -buffer-name=jump jump<cr>
  autocmd FileType unite call s:unite_settings()
  function! s:unite_settings()
    let b:SuperTabDisabled=1
    "imap <buffer> <C-j> <NOP>
    imap <buffer> <C-j>   <Plug>(unite_select_next_line)
    imap <buffer> <C-k>   <Plug>(unite_select_previous_line)
  endfunction

endif

" }}}

" Tmux Pane Nav {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" key mapping for window navigation
"
" If you're in tmux it'll keep going to tmux splits if you hit the end of
" your vim splits.
"
" For the tmux side see:
" https://github.com/aaronjensen/dotfiles/blob/e9c3551b40c43264ac2cd21d577f948192a46aea/tmux.conf#L96-L102
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"if exists('$TMUX')
"  function! TmuxOrSplitSwitch(wincmd, tmuxdir)
"    let previous_winnr = winnr()
"    silent! execute "wincmd " . a:wincmd
"    if previous_winnr == winnr()
"      call system("tmux select-pane -" . a:tmuxdir)
"      redraw!
"    endif
"  endfunction
"
"  let previous_title = substitute(system("tmux display-message -p '#{pane_title}'"), '\n', '', '')
"  let &t_ti = "\<Esc>]2;vim\<Esc>\\" . &t_ti
"  let &t_te = "\<Esc>]2;". previous_title . "\<Esc>\\" . &t_te
"
"  nnoremap <silent> <C-h> :call TmuxOrSplitSwitch('h', 'L')<cr>
"  nnoremap <silent> <C-j> :call TmuxOrSplitSwitch('j', 'D')<cr>
"  nnoremap <silent> <C-k> :call TmuxOrSplitSwitch('k', 'U')<cr>
"  nnoremap <silent> <C-l> :call TmuxOrSplitSwitch('l', 'R')<cr>
"else
"  "map <C-h> <C-w>h
"  "map <C-j> <C-w>j
"  "map <C-k> <C-w>k
"  "map <C-l> <C-w>l
"endif
" }}}

hi ShowMarksHLl ctermfg=11 ctermbg=none gui=bold guifg=blue guibg=lightblue
hi ShowMarksHLu ctermfg=11 ctermbg=none gui=bold guifg=blue guibg=lightblue
hi ShowMarksHLo ctermfg=11 ctermbg=none gui=bold guifg=blue guibg=lightblue
hi ShowMarksHLm ctermfg=11 ctermbg=none gui=bold guifg=blue guibg=lightblue
hi SignColumn   ctermbg=none

"command! -complete=shellcmd -nargs=+ Shell call s:RunShellCommand(<q-args>)
"function! s:RunShellCommand(cmdline)
"  echo a:cmdline
"  botright new
"  setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile nowrap
"  execute 'VimProcRead '. a:cmdline
"  setlocal nomodifiable
"  1
"endfunction
"
"
"
"command! -complete=shellcmd -nargs=+ VimProcBackground call s:run_vimproc_background(<q-args>)
"function! s:run_vimproc_background(cmdline)
"  let sub = vimproc#popen2(a:cmdline)
"  let res = ''
"  while !sub.stdout.eof
"    let res .= sub.stdout.read()
"  endwhile
"  let [cond, status] = sub.waitpid()
"  botright new
"  setlocal buftype=nofile bufhidden=wipe nobuflisted noswapfile nowrap
"  call append(0, split(res, '\r\n\|\r\|\n') + [string([cond, status])])
"endfunction
"
"command! -complete=shellcmd -nargs=+ Background call s:run_bg(<q-args>)
"function! s:run_bg(cmdline)
"  call vimproc#system_bg(a:cmdline . " >> /tmp/r 2>&1")
"endfunction

"let g:rspec_command = "Background rspec --format=Nc --format=doc --tty --color {spec}"


let g:rspec_command = "!bundle exec rspec --format=doc --tty --color {spec}"
command! -nargs=0 RspecCurrentFile :call RunCurrentSpecFile()
command! -nargs=0 RspecNearest :call RunNearestSpec()
command! -nargs=0 RspecLast :call RunLastSpec()
command! -nargs=0 RspecAll :call RunAllSpecs()

function RspecSpace()
  :wall
  :RspecLast
endfunction

nnoremap <leader>. :RspecLast<CR>
nnoremap <Space> :wall\|RspecLast<CR>


" Increment a column of number. Select a column of numbers (numbers should be
" the same), then C-a to have them incremented from the first number.
function! Incr()
    let a = line('.') - line("'<")
    let c = virtcol("'<")
    if a > 0
        execute 'normal! '.c.'|'.a."\<C-a>"
    endif
    normal `<
endfu

vnoremap <C-a> :call Incr()<CR>

"Kill Trailer Trash
autocmd BufWritePre * :Trim

" vim: set foldenable fdm=marker fmr={{{,}}}:
